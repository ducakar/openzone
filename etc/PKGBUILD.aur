# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgname=openzone
pkgver=0.3.81
pkgrel=1
pkgdesc='Simple cross-platform FPS/RTS game engine'
url="http://ducakar.github.com/openzone/"
license=('GPL3' 'ZLIB')
arch=('i686' 'x86_64')
depends=('libpulse' 'physfs' 'freeimage' 'libgl' 'luajit' 'openal' 'sdl_ttf')
optdepends=('openzone-data: game data'
            'espeak: speech synthesis'
            'libmad: MP3 playing'
            'faad2: AAC playing'
            'zip: building ZIP game data archives'
            'p7zip: building 7zip game data archives')
makedepends=('cmake' 'freeimage' 'libpulse' 'luajit' 'mesa' 'openal' 'physfs' 'sdl_ttf' 'libsquish')
source=("https://github.com/downloads/ducakar/openzone/${pkgname}-src-${pkgver}.tar.xz"
        "https://github.com/downloads/ducakar/openzone/${pkgname}-data-${pkgver}.tar.xz")
sha1sums=()

build()
{
  cd "${srcdir}/${pkgbase}-${pkgver}/build"

  cmake \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -msse3 -mfpmath=sse" \
    -D OZ_LUAJIT=1 \
    -D OZ_NONFREE=1 \
    ..

  make
}

package()
{
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ( cd build && make install DESTDIR="${pkgdir}" )

  install -Dm644 "src/ozCore/COPYING" "${pkgdir}/usr/share/licenses/openzone/COPYING.liboz"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/licences"
  install -m644 doc/*.html "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m644 doc/licences/* "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/licences"

  install -dm755 "${pkgdir}/usr/share/openzone"
  install -m644 "share/openzone/"*.zip "${pkgdir}/usr/share/openzone"
}
