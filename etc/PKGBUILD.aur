# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgname=openzone
pkgver=0.3.93
pkgrel=1
pkgdesc='Simple cross-platform FPS/RTS game engine'
url="http://ducakar.github.com/openzone/"
license=('GPL3' 'CCPL:by-nc-nd')
arch=('i686' 'x86_64')
depends=('alsa-lib' 'physfs' 'libgl' 'openal' 'libvorbis' 'lua' 'sdl2_ttf')
optdepends=('espeak: speech synthesis'
            'libmad: MP3 playback'
            'faad2: AAC playback')
makedepends=('cmake' 'mesa' 'libsquish')
source=("https://github.com/downloads/ducakar/openzone/${pkgname}-src-${pkgver}.tar.xz"
        "https://github.com/downloads/ducakar/openzone/${pkgname}-data-${pkgver}.tar.xz")
sha256sums=('SKIP'
            'SKIP')
noextract=("${pkgbase}-src-${pkgver}.tar.xz"
           "${pkgbase}-data-${pkgver}.tar.xz")

function prepare()
{
  tar xf ${pkgbase}-src-${pkgver}.tar.xz
  tar xf ${pkgbase}-data-${pkgver}.tar.xz
}

function build()
{
  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir -p build && cd build

  cmake \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_CXX_FLAGS="${CXXFLAGS/-O2} -msse3 -mfpmath=sse" \
    -D CMAKE_AR="/usr/bin/gcc-ar" \
    -D CMAKE_RANLIB="/usr/bin/gcc-ranlib" \
    -D CMAKE_CXX_FLAGS_RELEASE="-Ofast -flto" \
    -D OZ_NONFREE=1 \
    ..

  make
}

function package()
{
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ( cd build && make install DESTDIR="${pkgdir}" )

  rm -rf "${pkgdir}"/usr/{include,lib}

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/licences"
  install -m644 AUTHORS ChangeLog.md README.md TODO.md doc/*.html \
                "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m644 doc/licences/* "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/licences"

  install -dm755 "${pkgdir}/usr/share/openzone"
  install -m644 "share/openzone/"*.zip "${pkgdir}/usr/share/openzone"
}
