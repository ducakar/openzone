# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgbase=openzone
pkgname=('liboz' 'openzone-bin' 'openzone-data')
pkgver=0.3.0
_dataver=0.3.0
pkgrel=1
url="http://github.com/ducakar/openzone/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'faad2' 'freeimage' 'gcc>=4.6' 'git' 'libmad' 'libpulse' 'lua' 'mesa' 'openal' 'physfs' 'sdl_ttf')
#source=("http://openzone.googlecode.com/files/openzone-${_dataver}.7z")
#md5sums=('7c704605913f49fdaed587fc2f2cf8b8')
source=('ozbase.zip' 'openzone.zip')

_gitname='openzone'
_gitroot='git://github.com/ducakar/openzone.git'
_gittag="v${pkgver}"

build()
{
  cd ${srcdir}

  msg "git checkout for ${_gitroot} tag ${_gittag}"
  if [[ ! -d ${_gitname} ]]; then
    git clone ${_gitroot} ${_gitname}
    cd ${srcdir}/${_gitname} && git checkout #${_gittag}
  fi
  msg "git checkout done or server timeout"

  rm -rf build
  mkdir build && cd build

  cmake \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=RelWithDebInfo \
    ${srcdir}/${_gitname}

  make
}

package_liboz()
{
  pkgdesc='OpenZone core library'
  depends=('libpulse' 'physfs')

  cd ${srcdir}/build

  cmake \
    -D OZINST_LIBOZ=1 \
    -D OZINST_CLIENT=0 \
    -D OZINST_TOOLS=0 \
    -D OZINST_INFO=0 \
    -D OZINST_MENU=0 \
    -D OZINST_STANDALONE=0 \
    ${srcdir}/${_gitname}

  make install DESTDIR=${pkgdir}

  install -Dm644 ${pkgdir}/usr/share/doc/liboz/COPYING ${pkgdir}/usr/share/licenses/liboz
  rm -rf ${pkgdir}/usr/share/doc
}

package_openzone-bin()
{
  pkgdesc='A simple cross-platform FPS/RTS game engine'
  depends=('freeimage' 'libgl' 'liboz' 'lua' 'openal' 'sdl_ttf')

  cd ${srcdir}/build

  cmake \
    -D OZINST_LIBOZ=0 \
    -D OZINST_CLIENT=1 \
    -D OZINST_TOOLS=1 \
    -D OZINST_INFO=1 \
    -D OZINST_MENU=1 \
    -D OZINST_STANDALONE=0 \
    ${srcdir}/${_gitname}

  make install DESTDIR=${pkgdir}
  install -Dm644 share/openzone/ozbase.zip ${pkgdir}/usr/share/openzone/ozbase.zip
}

package_openzone-data()
{
  pkgver=${_dataver}
  pkgdesc='Runtime data for OpenZone game engine'
  arch=('any')

  cd ${srcdir}/build
  install -Dm644 share/openzone/openzone.zip ${pkgdir}/usr/share/openzone/openzone.zip
}
