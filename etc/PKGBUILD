# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgbase=openzone
pkgname=('liboz' 'openzone-bin' 'openzone-data' 'openzone')
pkgver=0.2.80
pkgrel=1
url="http://github.com/ducakar/openzone/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'doxygen' 'faad2' 'freeimage' 'gcc>=4.6' 'git' 'graphviz'
             'libmad' 'libpulse' 'lua' 'openal' 'physfs' 'sdl_ttf')
source=("openzone-src-${pkgver}.tar.xz"
        "openzone-data-ozbase-${pkgver}.tar.xz"
        "openzone-data-openzone-${pkgver}.tar.xz")
md5sums=()

build()
{
  ( cd ${srcdir}/openzone && doxygen etc/liboz/Doxyfile )

  mkdir -p ${srcdir}/build && cd ${srcdir}/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ${srcdir}/openzone

  make
}

package_liboz()
{
  pkgdesc='OpenZone core library'
  license=('custom')
  depends=('libpulse' 'physfs')

  cd ${srcdir}/build

  cmake \
    -DOZ_INSTALL_LIBOZ=1 \
    -DOZ_INSTALL_CLIENT=0 \
    -DOZ_INSTALL_TOOLS=0 \
    -DOZ_INSTALL_MENU=0 \
    -DOZ_INSTALL_STANDALONE=0 \
    ../openzone

  make install DESTDIR=${pkgdir}

  if [[ "$CARCH" == "x86_64" ]]; then
    mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
    sed 's|/usr/lib64|/usr/lib|g' -i ${pkgdir}/usr/lib/pkgconfig/liboz.pc
  fi

  install -Dm644 ../openzone/etc/liboz/COPYING ${pkgdir}/usr/share/licenses/liboz/COPYING
}

package_openzone-bin()
{
  pkgdesc='Simple cross-platform FPS/RTS game engine'
  depends=('freeimage' 'libgl' 'liboz' 'lua' 'openal' 'sdl_ttf')

  cd ${srcdir}/build

  cmake \
    -DOZ_INSTALL_LIBOZ=0 \
    -DOZ_INSTALL_CLIENT=1 \
    -DOZ_INSTALL_TOOLS=1 \
    -DOZ_INSTALL_MENU=1 \
    -DOZ_INSTALL_STANDALONE=0 \
    ${srcdir}/openzone

  make install DESTDIR=${pkgdir}

  install -Dm644 ../ozbase.zip ${pkgdir}/usr/share/openzone/ozbase.zip
  install -Dm644 ../openzone/AUTHORS ${pkgdir}/usr/share/doc/openzone/AUTHORS
  install -Dm644 ../openzone/ChangeLog ${pkgdir}/usr/share/doc/openzone/ChangeLog
  install -Dm644 ../openzone/TODO ${pkgdir}/usr/share/doc/openzone/TODO
  install -Dm644 ../openzone/README ${pkgdir}/usr/share/doc/openzone/README
  install -Dm644 ../openzone/README.sl ${pkgdir}/usr/share/doc/openzone/README.sl
}

package_openzone-data()
{
  pkgdesc='OpenZone game data'
  license=('CCPL:by-nc-sa')
  arch=('any')

  cd ${srcdir}/build

  install -Dm644 ${srcdir}/openzone.zip ${pkgdir}/usr/share/openzone/openzone.zip
}

package_openzone()
{
  pkgdesc='Simple cross-platform FPS/RTS game'
  depends=('openzone-bin' 'openzone-data')
  arch=('any')
}
