# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgbase=openzone
pkgname=('liboz' 'liboz-doc' 'libozdynamics' 'libozdynamics-doc' 'openzone' 'openzone-data'
         'openzone-doc')
pkgver=0.3.80
pkgrel=1
url="http://ducakar.github.com/openzone/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('alsa-lib' 'cmake' 'doxygen' 'faad2' 'freeimage' 'git' 'libmad' 'libpulse'
             'libvorbis' 'lua' 'mesa' 'openal' 'physfs' 'sdl_ttf' 'texlive-core')
source=("https://github.com/downloads/ducakar/openzone/openzone-src-$pkgver.tar.xz"
        "https://github.com/downloads/ducakar/openzone/openzone-data-$pkgver.tar.xz")
sha1sums=()

build()
{
  cd "$srcdir/$pkgbase-$pkgver"

  mkdir -p build && cd build
  cmake \
    -D CMAKE_BUILD_TYPE="Release" \
    -D CMAKE_INSTALL_PREFIX="/usr" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -msse3 -mfpmath=sse" \
    -D OZ_SHARED_LIBS="1" \
    -D OZ_NONFREE="1" \
    ..

  make doc
  make
}

package_liboz()
{
  pkgdesc='OpenZone Core Library'
  license=('ZLIB')
  depends=('alsa-lib' 'gcc-libs' 'physfs')

  cd "$srcdir/$pkgbase-$pkgver"

  ( cd build && make install DESTDIR="$pkgdir" )

  rm -rf "$pkgdir/usr/"{bin,share}
  rm -rf "$pkgdir/usr/include/ozdynamics"
  rm -rf "$pkgdir/usr/lib/libozdynamics.so"*

  install -Dm644 "src/oz/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_liboz-doc()
{
  pkgdesc='OpenZone Core Library documentation'
  license=('ZLIB')
  arch=('any')
  depends=('liboz')

  cd "$srcdir/$pkgbase-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/liboz"
  cp -r "doc/doxygen.liboz/html" "$pkgdir/usr/share/doc/liboz/doxygen"
}

package_libozdynamics()
{
  pkgdesc='OpenZone Dynamics Library'
  license=('ZLIB')
  depends=('liboz')

  cd "$srcdir/$pkgbase-$pkgver"

  ( cd build && make install DESTDIR="$pkgdir" )

  rm -rf "$pkgdir/usr/"{bin,share}
  rm -rf "$pkgdir/usr/include/oz"
  rm -rf "$pkgdir/usr/lib/liboz.so"*

  install -Dm644 "src/ozdynamics/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_libozdynamics-doc()
{
  pkgdesc='OpenZone Dynamics Library documentation'
  license=('ZLIB')
  arch=('any')
  depends=('libozdynamics')

  cd "$srcdir/$pkgbase-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/libozdynamics"
  cp -r "doc/doxygen.libozdynamics/html" "$pkgdir/usr/share/doc/libozdynamics/doxygen"
}

package_openzone()
{
  pkgdesc='Simple cross-platform FPS/RTS game engine'
  depends=('freeimage' 'libgl' 'liboz' 'libvorbis' 'lua' 'openal' 'sdl_ttf')
  optdepends=('openzone-data: game data'
              'espeak: speech synthesis'
              'libmad: MP3 playing'
              'faad2: AAC playing'
              'zip: building ZIP game data archives'
              'p7zip: building 7zip game data archives')

  cd "$srcdir/$pkgbase-$pkgver"

  ( cd build && make install DESTDIR="$pkgdir" )

  rm -rf "$pkgdir/usr/"{include,lib}

  install -dm755 "$pkgdir/usr/share/doc/openzone"
  install -m644 AUTHORS README.md ChangeLog.md doc/*.html "$pkgdir/usr/share/doc/openzone"
}

package_openzone-data()
{
  pkgdesc='OpenZone game data'
  license=('custom')
  arch=('any')

  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/share/openzone"
  install -m644 "share/openzone/"*.zip "$pkgdir/usr/share/openzone"
}

package_openzone-doc()
{
  pkgdesc='OpenZone engine documentation'
  arch=('any')
  depends=('openzone')

  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/share/doc/openzone"
  cp -r "doc/doxygen/html" "$pkgdir/usr/share/doc/openzone/doxygen"
}
