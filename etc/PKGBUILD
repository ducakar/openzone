# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgbase=openzone
pkgname=('liboz' 'openzone' 'openzone-data')
pkgver=0.3.80
pkgrel=1
url="http://ducakar.github.com/openzone/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'freeimage' 'git' 'libpulse' 'luajit' 'mesa' 'openal' 'physfs' 'sdl_ttf')
source=("https://github.com/downloads/ducakar/openzone/openzone-src-$pkgver.tar.xz"
        "https://github.com/downloads/ducakar/openzone/openzone-data-$pkgver.tar.xz")
sha1sums=()

build()
{
  cd "$srcdir/$pkgbase-$pkgver/build"

  cmake \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -msse3 -mfpmath=sse" \
    -D OZ_SHARED_LIBS=1 \
    -D OZ_LUAJIT=1 \
    -D OZ_NONFREE=1 \
    ..

  make
}

package_liboz()
{
  pkgdesc='OpenZone Core and Dynamics libraries'
  license=('ZLIB')
  depends=('libpulse' 'physfs')

  cd "$srcdir/$pkgbase-$pkgver"

  ( cd build && make install DESTDIR="$pkgdir" )

  rm -rf "$pkgdir/usr/"{bin,share}

  install -Dm644 "src/ozCore/COPYING" "$pkgdir/usr/share/licenses/liboz/COPYING"
}

package_openzone()
{
  pkgdesc='Simple cross-platform FPS/RTS game engine'
  depends=('freeimage' 'libgl' 'liboz' 'luajit' 'openal' 'sdl_ttf')
  optdepends=('openzone-data: game data'
              'espeak: speech synthesis'
              'libmad: MP3 playing'
              'faad2: AAC playing'
              'zip: building ZIP game data archives'
              'p7zip: building 7zip game data archives')

  cd "$srcdir/$pkgbase-$pkgver"

  ( cd build && make install DESTDIR="$pkgdir" )

  rm -rf "$pkgdir/usr/"{lib,include,share/doc}

  install -dm755 "$pkgdir/usr/share/doc/openzone/licences"
  install -m644 AUTHORS README.md ChangeLog.md doc/*.html "$pkgdir/usr/share/doc/openzone"
  install -m644 doc/licences/* "$pkgdir/usr/share/doc/openzone/licences"
}

package_openzone-data()
{
  pkgdesc='OpenZone game data'
  license=('custom')
  arch=('any')

  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/share/openzone"
  install -m644 "share/openzone/"*.zip "$pkgdir/usr/share/openzone"
}
