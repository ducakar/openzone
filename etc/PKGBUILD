# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgbase=openzone
pkgname=('liboz' 'openzone' 'openzone-data' 'openzone-doc')
pkgver=0.3.80
pkgrel=1
url="http://ducakar.github.com/openzone/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'doxygen' 'faad2' 'freeimage' 'gcc' 'git' 'graphviz'
             'libmad' 'libvorbis' 'lua' 'openal' 'physfs' 'sdl_ttf')
source=("https://github.com/downloads/ducakar/openzone/openzone-src-${pkgver}.tar.xz"
        "https://github.com/downloads/ducakar/openzone/openzone-data-${pkgver}.tar.xz")
sha1sums=()

build()
{
  mkdir -p ${srcdir}/build && cd ${srcdir}/build

   cmake \
     -D CMAKE_BUILD_TYPE=Release \
     -D CMAKE_INSTALL_PREFIX=/usr \
     -D OZ_SHARED_LIBOZ=1 \
     -D OZ_NONFREE=1 \
     ../openzone

  make doc
  make
}

package_liboz()
{
  pkgdesc='OpenZone core library'
  license=('ZLIB')
  depends=('gcc-libs' 'physfs')
  optdepends=('pulseaudio: warning bell')

  cd ${srcdir}/build

  make install DESTDIR=${pkgdir}

  rm -rf ${pkgdir}/usr/bin
  rm -rf ${pkgdir}/usr/share/{applications,pixmaps}

  mkdir -p ${pkgdir}/usr/share/doc/liboz
  cp -r ../openzone/doc/doxygen.liboz/html ${pkgdir}/usr/share/doc/liboz/doxygen

  install -Dm644 ../openzone/src/oz/COPYING ${pkgdir}/usr/share/licenses/liboz/COPYING
}

package_openzone()
{
  pkgdesc='Simple cross-platform FPS/RTS game engine'
  depends=('freeimage' 'libgl' 'liboz' 'libvorbis' 'lua' 'openal' 'sdl_ttf')
  optdepends=('openzone-data: game data'
              'zip: for building ZIP game data archives'
              'p7zip: for building 7zip game data archives')

  cd ${srcdir}/build

  make install DESTDIR=${pkgdir}

  rm -rf ${pkgdir}/usr/{include,lib}

  cd ${srcdir}/openzone

  install -dm755 ${pkgdir}/usr/share/doc/openzone
  install -m644 AUTHORS README.md ChangeLog.md doc/*.html ${pkgdir}/usr/share/doc/openzone
}

package_openzone-data()
{
  pkgdesc='OpenZone game data'
  license=('custom')
  arch=('any')

  install -dm755 ${pkgdir}/usr/share/openzone
  install -m644 ${srcdir}/openzone/share/openzone/*.zip ${pkgdir}/usr/share/openzone
}

package_openzone-doc()
{
  pkgdesc='OpenZone engine documentation'
  arch=('any')

  install -dm755 ${pkgdir}/usr/share/doc/openzone
  cp -r ${srcdir}/openzone/doc/doxygen/html ${pkgdir}/usr/share/doc/openzone/doxygen
}
